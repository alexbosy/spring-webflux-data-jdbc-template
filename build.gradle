plugins {
	id 'org.springframework.boot' version '3.0.5'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'java'
	id 'groovy'
	id 'org.unbroken-dome.test-sets' version '4.1.0'
}

repositories {
	mavenCentral()
	maven {
		url "https://plugins.gradle.org/m2/"
	}
}

group = 'dev.abosy'
version = (findProperty('version') == 'unspecified') ? 'latest' : version

jar {
	archivesBaseName = 'some-service'
}

springBoot {
	buildInfo {
		properties {
			additional = [
					time: new Date().format('dd-MM-yyyy:HH:mm:ss zzz'),
					OS  : "${System.properties['os.name']} (${System.properties['os.version']})",
					JVM : "Vendor: ${System.properties['java.specification.vendor']}, Version: ${System.properties['java.version']}"
			]
		}
	}
}

testSets {
	integrationTest { dirName = 'test-integration' }
	acceptanceTest { dirName = 'test-acceptance' }
}

tasks.withType(Test) {
	systemProperties System.properties
}

tasks.withType(Test) {
	useJUnitPlatform()
}

test {
	systemProperties System.properties
	useJUnitPlatform()
}

tasks.register('t') { dependsOn test }
tasks.register('it') { dependsOn integrationTest }
tasks.register('at') { dependsOn acceptanceTest }

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-jetty'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'org.liquibase:liquibase-core'
	implementation 'org.eclipse.jetty:jetty-server:12.0.2'

	implementation 'com.google.guava:guava:31.1-jre'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'

	runtimeOnly 'org.postgresql:postgresql'

	testImplementation 'org.apache.groovy:groovy:4.0.15'
	testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'
	testImplementation 'org.spockframework:spock-spring:2.4-M1-groovy-4.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'

	acceptanceTestImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

configurations {
	all*.exclude module: 'spring-boot-starter-logging'
}

tasks.named("bootBuildImage") {
	environment = ["BP_JVM_VERSION" : "17.*"]
	imageName = "${project.name}:${version}"
	buildpacks = ["gcr.io/paketo-buildpacks/azul-zulu", "urn:cnb:builder:paketo-buildpacks/java"]
	tags = ["${project.name}:latest"]
}



